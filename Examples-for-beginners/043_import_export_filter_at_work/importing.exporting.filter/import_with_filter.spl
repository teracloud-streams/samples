namespace importing.exporting.filter;

composite import_with_filter
{
	type
		SimpleTransaction = tuple<int64 number, rstring string>;

	graph
		// Import the stream emitted by the dynamic_export main composite.
		stream<SimpleTransaction> ImportedSimpleTxStream = Import()
		{
			param
				// To begin with, set an import filter expression that will not find a match with the exported stream from another composite.
				filter: number < 0l;
				subscription: exportedStreams == 1l;
		}

		// Let us have a Beacon to periodically send a signal to change the input port import filter expression.
		stream<int32 filterExpressionChangeSignal> ImportFilterExpressionChangeSignal = Beacon()
		{
			param
				period: 5.0;
		}

		// Let us change the import subscription value in this Custom operator.
		stream<SimpleTransaction> SimpleTxStreamForOutput = Custom(ImportFilterExpressionChangeSignal as IFECS;
			ImportedSimpleTxStream as ISTS)
		{
			logic
				state:
				{
					mutable int32 _secondsElapsedSinceImportFilterExpressionWasChanged = 0l;
					mutable boolean _matchExportedStreamAttributeValues = false;
					mutable rstring _newFilterExpression = "";
				} 
				onTuple IFECS:
				{
					// The other main composite in this application (export_with_filter) will keep sending
					// one exported tuple every 5 seconds. That tuple contains two attributes: int64 number, rstring string.
					// Every tuple will carry increasing value for the number attribute.
					//
					//
					// Similarly, in the current main composite, where you are now (import_with_filter), we will
					// write some code below to demonstrate how import filter expression can also be 
					// dynamically changed using a new API available in Streams 3.0.
					// In order to do that, after every 20 seconds, we will arbitrarily change the
					// filter expression so as to toggle the matching of the exported stream tuple attribute.
					// i.e. for one 20 second block, we will not match any exported stream tuple attribute values and
					// in the next 20 second block, we will match with the exported stream tuple attribute values.
					// We are doing this logic just to show the reader of this application, how the
					// import filter expression can be changed dynamically.
					_secondsElapsedSinceImportFilterExpressionWasChanged += 5;

					if (_secondsElapsedSinceImportFilterExpressionWasChanged >= 20)
					{
						// It is time to a change the import filter expression value.
						// Reset the timer flag to 0 to wait for the next 20 seconds of time to pass by.
						_secondsElapsedSinceImportFilterExpressionWasChanged = 0;

						// On every import filter expression change event, toggle either to match or not match with the exported stream tuple attribute.
						if (_matchExportedStreamAttributeValues == false)
						{
							// Set the import filter expression such that we will match the exported stream tuple attribute.
							_matchExportedStreamAttributeValues = true;
							_newFilterExpression = "number > 0";
						}
						else
						{
							// Set the import filter expression such that we will not match the exported stream tuple attribute.
							// Please note that, we are doing this arbitrary logic just to showcase
							// SPL change filter expression API.
							_matchExportedStreamAttributeValues = false;
							_newFilterExpression = "number < 0";
						}
						// Let us keep changing the import filter expression value periodically.
						int32 rc = setInputPortImportFilterExpression(_newFilterExpression, 1u);

						if (rc != 0)
						{
							// Log the error and abort the application.
							log(Sys.error, "New Filter Expression=" + _newFilterExpression);
							log(Sys.error,
								"setInputPortImportFilterExpression API failed with return code " + (rstring) rc);
							abort();
						}
					} // 
				} 
				onTuple ISTS:
				{
					// Send it on the first output port index (New submit API in Streams 3.0)
					submit(ISTS, 0u);
				} 
		}

		() as Sink1 = Custom(SimpleTxStreamForOutput as STSFO)
		{
			logic
				onTuple STSFO:
				{
					printStringLn("SimpleTxStream = " + (rstring) STSFO);
				}
		}
}
